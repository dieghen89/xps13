# Mainteiner: dieghen89 <dieghen89 at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Powalowski  <tpowa@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=xf86-input-synaptics-xswipe-git
pkgver=r1409.f7199b6
pkgrel=1
pkgdesc="Synaptics driver for notebook touchpads - Forked branch with -m flag forward ported and 4/5 finger detectable"
arch=('i686' 'x86_64')
license=('custom')
url="https://github.com/felipejfc/xserver-xorg-input-synaptics"
depends=('libxtst' 'mtdev' 'libevdev')
makedepends=('xorg-server-devel' 'X-ABI-XINPUT_VERSION=20' 'libxi' 'libx11' 'resourceproto' 'scrnsaverproto' 'git')
conflicts=('xorg-server<1.15.0' 'X-ABI-XINPUT_VERSION<20' 'X-ABI-XINPUT_VERSION>=21')
replaces=('synaptics' 'xf86-input-synaptics')
provides=('synaptics' 'xf86-input-synaptics')
conflicts=('synaptics' 'xf86-input-synaptics')
groups=('xorg-drivers' 'xorg')
backup=('etc/X11/xorg.conf.d/50-synaptics.conf')
source=("${pkgname}::git+https://github.com/felipejfc/xserver-xorg-input-synaptics.git")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ${srcdir}/${pkgname}  
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${pkgname}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/etc/X11/xorg.conf.d"
  install -m644 conf/50-synaptics.conf "${pkgdir}/etc/X11/xorg.conf.d/"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
  rm -rf "${pkgdir}/usr/share/X11"
}
